apply plugin: 'groovy'

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}:indy"
}

compileGroovy {
    groovyOptions.configurationScript = file( "$rootDir/gradle/groovyCompilerConfiguration.groovy" )
}

compileGroovy.groovyOptions.optimizationOptions['indy'] = true
compileTestGroovy.groovyOptions.optimizationOptions['indy'] = true

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

compileGroovy.options*.compilerArgs = ['-Xlint:deprecation','-encoding', 'UTF-8']
compileTestGroovy.options*.compilerArgs = ['-Xlint:deprecation','-encoding', 'UjTF-8']

defaultTasks << 'clean'
defaultTasks << 'build'

ext.sharedManifest = manifest {
    attributes( 'Implementation-Title': project.name, 'Implementation-Version': project.version )
}

jar {
    manifest = project.manifest {
        from sharedManifest
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    manifest = project.manifest {
        from sharedManifest
    }
}

task testSourcesJar(type: Jar, dependsOn: testClasses) {
    classifier = 'test-sources'
    from sourceSets.test.allSource
    manifest = project.manifest {
        from sharedManifest
    }
}

task testBinariesJar(type: Jar, dependsOn: testClasses) {
    classifier = 'test'
    from sourceSets.test.output
    manifest = project.manifest {
        from sharedManifest
    }
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from project.docsDir.path + '/groovydoc'
    manifest = project.manifest {
        from sharedManifest
    }
}

groovydoc {
    docTitle = "docTitle: $project.name $version"
    footer = "footer: $project.name $version"
    header = "header: $project.name $version"
    windowTitle = "windowTitle: $project.name $version"
    includePrivate = false
    overview = 'overview.html'
    use = true
}

artifacts {
    archives sourcesJar
    archives testSourcesJar
    archives testBinariesJar
    archives groovydocJar
}

